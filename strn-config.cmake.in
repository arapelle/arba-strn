# strn-config.cmake - package configuration file

@PACKAGE_INIT@

include(${CMAKE_CURRENT_LIST_DIR}/strn.cmake)

get_target_property(strn-CONFS strn IMPORTED_CONFIGURATIONS)
if(strn-CONFS)
    get_target_property(inc-dirs strn INTERFACE_INCLUDE_DIRECTORIES)
    if(NOT inc-dirs)
        get_target_property(inc-dirs strn-static INTERFACE_INCLUDE_DIRECTORIES)
    endif()
    set_target_properties(strn PROPERTIES INCLUDE_DIRECTORIES ${inc-dirs})
    set(strn_INCLUDE_DIRECTORIES ${inc-dirs})
    foreach(strn-CONF ${strn-CONFS})
        # Get shared
        get_target_property(shared-path strn IMPORTED_LOCATION_${strn-CONF})
        get_filename_component(shared-dir ${shared-path} DIRECTORY)
        get_filename_component(shared-name ${shared-path} NAME)
        # Get static
        get_target_property(static-path strn-static IMPORTED_LOCATION_${strn-CONF})
        get_filename_component(static-dir ${static-path} DIRECTORY)
        get_filename_component(static-name ${static-path} NAME)
        # Set target properties
        set_target_properties(strn PROPERTIES
            LIBRARY_OUTPUT_DIRECTORY_${strn-CONF} ${shared-dir}
            LIBRARY_OUTPUT_NAME_${strn-CONF} ${shared-name}
            ARCHIVE_OUTPUT_DIRECTORY_${strn-CONF} ${static-dir}
            ARCHIVE_OUTPUT_NAME_${strn-CONF} ${static-name}
            )
        set(strn_LIBRARY_${strn-CONF} ${shared-path})
        set(strn_ARCHIVE_${strn-CONF} ${static-path})
        message(STATUS "Found strn ${strn-CONF}: (found version \"${strn_VERSION}\"): ${shared-name} ${static-name}")
    endforeach()
endif()
